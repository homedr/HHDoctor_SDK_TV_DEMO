import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'bugly'

apply plugin: 'kotlin-android-extensions'

def sdkVersion = "3.0.4.09241452"

def supportVersion = "27.1.1"

def appVer = "2.4.8"

// versionCode按时间自增
static def increasedVersionCode() {
    return (int) (System.currentTimeMillis() / 1000 / 60)
}

static def buildTime() {
    def df = new SimpleDateFormat("MMddHH")
    return df.format(new Date())
}

bugly {
    appId = '5b5d3f2384' // 注册时分配的App ID
    appKey = 'f3f1cdd4-a6c0-4263-85fe-af1cc38d6e54' // 注册时分配的App Key
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.hhmedic.app.patient.demo.tv"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode increasedVersionCode()
        versionName "${appVer}.${buildTime()}"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        ndk {
            //设置支持的SO库架构
            abiFilters "armeabi-v7a"
        }
    }

    signingConfigs {
        keyStore {
            keyAlias 'hh'
            keyPassword 'hh2017'
            storeFile file('hh.jks')
            storePassword 'hh2017'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.keyStore
            buildConfigField('boolean', "isDev", "${rootProject.ext.isDev}")
            manifestPlaceholders = [versionName: defaultConfig.versionName]
        }
        debug {

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.keyStore
            buildConfigField('boolean', "isDev", "${rootProject.ext.isDev}")
            manifestPlaceholders = [versionName: defaultConfig.versionName]
        }
    }

    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libsecsdk.so'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    android.applicationVariants.all { variant ->

        variant.outputs.all
                {

                    outputFileName = "hh_github_tv_sdk_demo_${defaultConfig.versionName}.apk"

                }
    }
}

configurations.all {
    resolutionStrategy {
        force "com.android.support:recyclerview-v7:$supportVersion"
        force "com.android.support:appcompat-v7:$supportVersion"
        force "com.android.support:support-v4:$supportVersion"
        force "com.android.support:design:$supportVersion"
        force "com.android.support:support-vector-drawable:$supportVersion"
        force "com.android.support:cardview-v7:$supportVersion"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.hhmedic.android.sdk:hh_tv:$sdkVersion"
    implementation 'androidx.appcompat:appcompat:1.0.0'
    api 'com.yanzhenjie:permission:2.0.0-rc12'
    api 'com.qmuiteam:qmui:1.4.4'
    api 'cn.yipianfengye.android:zxing-library:2.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}
